defmodule Klife.Producer.Murmur2Test do
  use ExUnit.Case, async: true

  alias Klife.Producer.Murmur2

  # Reference values from Apache Kafka's UtilsTest.testMurmur2
  # https://github.com/apache/kafka/blob/trunk/clients/src/test/java/org/apache/kafka/common/utils/UtilsTest.java
  test "matches Kafka murmur2 reference values" do
    assert Murmur2.hash("21") == -973_932_308
    assert Murmur2.hash("foobar") == -790_332_482
    assert Murmur2.hash("a-little-bit-long-string") == -985_981_536
    assert Murmur2.hash("a-little-bit-longer-string") == -1_486_304_829
    assert Murmur2.hash("lkjh234lh9fiuh90y23oiuhsafujhadof229phr9h19h89h8") == -58_897_971
    assert Murmur2.hash("abc") == 479_470_107
  end

  # Regression test inspired by Kafka's testMurmur2Checksum which hashes 100k+
  # random byte arrays generated by Java's SplittableRandom and checks the sum.
  # I first validated correctness against Kafka's exact test by generating the
  # same byte arrays via a Java program and comparing the checksum (0xc3b8cf7c99fc).
  # Once confirmed, I replaced it with this self-contained version using Erlang's
  # :rand with a fixed seed to avoid carrying a 48MB binary fixture in the repo.
  test "regression checksum over 1000 random inputs" do
    :rand.seed(:exsss, {1, 2, 3})

    checksum =
      Enum.reduce(1..10_000, 0, fn _i, acc ->
        data = :rand.bytes(Enum.random(1..1000))
        hash = Murmur2.hash(data)
        unsigned = if hash < 0, do: hash + 0x100000000, else: hash
        acc + unsigned
      end)

    assert checksum == 21_469_448_268_819
  end
end
